import wollok.game.*
import simulacion.*
import manzanas.*
import personas.*

object pandemiaGameConfig {
	
	
	method height() = 4
	method width() = 6
	method title() = "Pandemia Game"
	
	
	method configureGame() {
		self.configureGameParameters()
		self.agregarManzanas()
		keyboard.s().onPressDo ({
				simulacion.siguienteDia()
				game.say(indicador,"Termino el dia")
		})
		indicador.position(0,0)
		game.addVisual(indicador)
		keyboard.up().onPressDo{indicador.moveteArriba()}
		keyboard.down().onPressDo{indicador.moveteAbajo()}
		keyboard.left().onPressDo{indicador.moveteIzquierda()}
		keyboard.right().onPressDo{indicador.moveteDerecha()}
		keyboard.i().onPressDo{game.say(
			indicador,"Día: " + simulacion.diaActual() +
			" personas " + simulacion.cantidadPersonas() +
			" Infectados " + simulacion.evaluarInfectados() +
			" Con Sintomas " + simulacion.evaluarSintomas()
			
		)}
		keyboard.a().onPressDo{
			simulacion.agregarInfectado()
		}
		
		// agregar las teclas de acciones
		// después, al agente de salud y sus teclas de acciones y movimiento
	}
		
	method configureGameParameters() {
		game.height(self.height())
		game.width(self.width())
		game.title(self.title())
	}
	
	method agregarManzanas() {
		(1..game.width()).forEach({x => 
			(1..game.height()).forEach({y => 
				const nuevaManzana = simulacion.crearManzana()
				nuevaManzana.position(game.at(x-1,y-1))
				simulacion.agregarManzana(nuevaManzana)
				
				game.addVisual(nuevaManzana)
			})
		})
	}
	
	
}

program pandemiaGame {
	pandemiaGameConfig.configureGame()
	game.start()

}

