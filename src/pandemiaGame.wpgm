import wollok.game.*
import simulacion.*
import manzanas.*
import personas.*

object pandemiaGameConfig {
	
	
	method height() = 4
	method width() = 6
	method title() = "Pandemia Game"
	
	
	method configureGame() {
		self.configureGameParameters()
		self.agregarManzanas()
		keyboard.s().onPressDo ({
				simulacion.siguienteDia()
				console.println("Terminó el día")
		})
		keyboard.i().onPressDo{ console.println(
			"Día: " + simulacion.diaActual() +
			" | Personas: " + simulacion.cantidadPersonas() +
			" | Infectados: " + simulacion.evaluarInfectados() +
			" | Infectados con síntomas: " + simulacion.evaluarSintomas()
			
		)}
		
		agenteDeSalud.position(0,0)
		game.addVisual(agenteDeSalud)
		
		keyboard.up().onPressDo{agenteDeSalud.moveteArriba()}
		keyboard.down().onPressDo{agenteDeSalud.moveteAbajo()}
		keyboard.left().onPressDo{agenteDeSalud.moveteIzquierda()}
		keyboard.right().onPressDo{agenteDeSalud.moveteDerecha()}
		
		keyboard.a().onPressDo{
			simulacion.agregarInfectado()
			console.println("Llego un infectado al barrio")
		}
		keyboard.x().onPressDo{
			agenteDeSalud.aislarAInfectades()
			console.println("Todxs lxs infectadxs con sintomas fueron aisladxs")
		}
		keyboard.z().onPressDo{
			agenteDeSalud.convencerATodes()
			console.println("Ahora todxs lxs habitantes de esta manzana respetan la cuarentena")
		}
		
		
		// agregar las teclas de acciones
		// después, al agente de salud y sus teclas de acciones y movimiento
	}
		
	method configureGameParameters() {
		game.height(self.height())
		game.width(self.width())
		game.title(self.title())
	}
	
	method agregarManzanas() {
		(1..game.width()).forEach({x => 
			(1..game.height()).forEach({y => 
				const nuevaManzana = simulacion.crearManzana()
				nuevaManzana.position(game.at(x-1,y-1))
				simulacion.agregarManzana(nuevaManzana)
				
				game.addVisual(nuevaManzana)
			})
		})
	}
	
	
}

program pandemiaGame {
	pandemiaGameConfig.configureGame()
	game.start()

}

